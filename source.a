;Comandare in faza a unui tiristor pentru a efectua
;redresare monoalternanta
;Bejdak Martina, Balanescu Adrian, Chirap Andrei - E115
;Programul functioneaza
	
	ORG 0000H
	LJMP MAIN
	ORG 000BH	;adresa intrerupere timer0
	LJMP TIM0
	ORG 001BH	;adresa intrerupere timer1
	LJMP TIM1	

	ORG 100H
MAIN:	;--testing
	MOV DPTR, #1000h;populare valori la adresa 1000h pentru testare aplicatie
	MOV A, #17h
	MOVX @DPTR, A	; Talfa = 1ms
	INC DPTR
	MOV A, #0FCh
	MOVX @DPTR, A
	;--testing

	ACALL INIT	;apelare subrutina de initializare
 	SJMP $		;bucla de asteptare
	
	ORG 150h
INIT:	CLR EA		;dezactivare globala a intreruperilor
	MOV TMOD, #11H	;configurare Timer0 si Timer1 in mod de lucru 1 pe 16b, temporizator cu declansare soft
	
	ACALL getTa;	;incarcare valoare Talfa in R2 si R3
	MOV TL0, R1	;LSB pentru Talfa contorizat de timer0 #9BH
	MOV TH0, R2	;MSB pentru Talfa contorizat de timer0 #0FFH

	MOV TL1, #0DFH	;LSB pentru T contorizat de timer1
	MOV TH1, #0B1H	;MSB pentru T contorizat de timer1
	
	MOV R0, #0	;contor stare timer1

	SETB ET0	;validare intrerupere timer0
	SETB ET1	;validare intrerupere timer1

	MOV P1, #0H	;initializare port0 cu valoarea 0

	SETB TR0	;pornire timer0
	SETB TR1	;pornire timer1

	SETB EA		;activare globala intreruperi
	RET

	ORG 200h
getTa:	;citire Talfa din memoria externa de la adresa 1000h in R1	
	MOV DPTR, #1000h
	MOVX A, @DPTR
	MOV R1, A	;R1 = Talfa LSB
	INC DPTR
	MOVX A, @DPTR
	MOV R2, A	;R2 = Talfa MSB
	;end citire din mem externa

	RET

	ORG 250H
TIM0:	CLR TR0		;dezactivare timer0
	CJNE R0, #0, state1
	CJNE R0, #1, state0
	RETI
	
state0:	SETB P1.1	;activare impuls pentru tiristor
	MOV TL0, #17H	;LSB pentru Timpuls contorizat de timer0, Timpuls = 1ms
	MOV TH0, #0FCH	;MSB pentru Timpuls contorizat de timer0
	SETB TR0	;pornire contorizare Ti 
	MOV R0, #1
	
	RETI

state1:	CLR P1.1	;dezactivare impuls pentru tiristor
	MOV R0, #0

	RETI
	

	ORG 300H
TIM1:	CLR TR0
	CLR TR1

	ACALL getTa;	;incarcare valoare Talfa in R2 si R3
	MOV TL0, R1	;LSB pentru Talfa contorizat de timer0
	MOV TH0, R2 	;MSB pentru Talfa contorizat de timer0
	
        MOV TL1, #0DEH	;LSB pentru T contorizat de timer1, 20ms
	MOV TH1, #0B1H	;MSB pentru T contorizat de timer1

	MOV R0, #0	;contor stare timer1
	
	SETB TR0	;pornire timer0
	SETB TR1	;pornire timer1

	RETI

	END
